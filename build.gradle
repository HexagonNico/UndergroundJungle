subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    // Group id and mod version
    group = modGroupId
    version = modVersion

    // Mojang ships Java 17 to end users in 1.18+
    java.toolchain.languageVersion = JavaLanguageVersion.of(17)

    // Repositories for subprojects
    repositories {
        // Replace with mavenCentral() to use the published version
        flatDir {
            dirs '../libs/'
        }
    }

    // Jar manifest attributes
    jar {
        manifest {
            attributes([
                    "Specification-Title": rootProject.name,
                    "Specification-Vendor": 'HexagonNico',
                    "Specification-Version": project.version,
                    "Implementation-Title": project.name,
                    "Implementation-Version": project.jar.archiveVersion,
                    "Implementation-Vendor": 'HexagonNico',
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
            ])
        }
    }

    publishing {
        publications {
            register('mavenJava', MavenPublication) {
                artifact jar
            }
        }
        repositories {
            maven {
                url "file://${project.projectDir}/mcmodsrepo"
            }
        }
    }
}