import net.darkhax.curseforgegradle.TaskPublishCurseForge

plugins {
    // Idea plugin
    id 'idea'
    // Fabric plugin
    id 'fabric-loom' version '1.2-SNAPSHOT'
    // CurseForge publishing
    id 'net.darkhax.curseforgegradle' version '1.1.15'
    // Modrinth publishing
    id "com.modrinth.minotaur" version "2.+"
}

base {
    // Archive name for the fabric mod
    archivesName = 'underground-jungle-fabric'
}

loom {
    // Specify mixin refmap
    mixin {
        defaultRefmapName.set "mixin.undergroundjungle.refmap.json"
    }
    // Fabric run configurations
    runs {
        client {
            client()
            ideConfigGenerated(true)
            runDir('../.minecraft')
        }
    }
    // Add the common module to the classpath to allow fabric to find resources
    mods {
        "${modId}" {
            sourceSet sourceSets.main
            sourceSet project(':common').sourceSets.main
        }
    }
}

dependencies {
    // Minecraft dependency
    minecraft "com.mojang:minecraft:${minecraftVersion}"
    mappings loom.officialMojangMappings()
    // Fabric loader and api dependency
    modImplementation "net.fabricmc:fabric-loader:${fabricVersion}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabricApiVersion}+${minecraftVersion}"
    // Common module dependency
    implementation project(':common')
    // Phantom library dependency
    modImplementation "curse.maven:phantom-loader-958545:5026335"
    annotationProcessor "curse.maven:phantom-loader-958545:5026335"
    annotationProcessor "net.fabricmc:fabric-loader:${fabricVersion}"
    annotationProcessor "io.github.phantomloader:processor-fabric:${phantomVersion}"
    // Optional TerraBlender dependency
    modCompileOnly "com.github.glitchfiend:TerraBlender-fabric:${minecraftVersion}-${terraBlenderVersion}"
    annotationProcessor "com.github.glitchfiend:TerraBlender-fabric:${minecraftVersion}-${terraBlenderVersion}"
}

tasks.withType(JavaCompile).configureEach {
    // Copy sources from the common module
    source project(':common').sourceSets.main.allSource
    // Pass options used by the annotation processor
    options.compilerArgs.add "-AfabricVersion=${fabricVersion}"
    options.compilerArgs.add "-AphantomVersion=${phantomVersion}"
    options.compilerArgs.add "-AminecraftVersion=${minecraftVersion}"
    options.compilerArgs.add "-AmodId=${modId}"
    options.compilerArgs.add "-AmodGroupId=${modGroupId}"
    options.compilerArgs.add "-AmodVersion=${modVersion}"
    options.compilerArgs.add "-AmodName=${modName}"
    options.compilerArgs.add "-AmodLicense=${modLicense}"
    options.compilerArgs.add "-AmodVersion=${modVersion}"
    options.compilerArgs.add "-AmodAuthors=${modAuthors}"
    options.compilerArgs.add "-AmodDescription=${modDescription}"
    options.compilerArgs.add "-AmodSource=${modSource}"
    options.compilerArgs.add "-AmodMixin=${modMixin}"
}

processResources {
    // Copy resources from the common module
    from project(':common').sourceSets.main.resources
}

var changelogFile = rootProject.file('changelog.md')
var changelogText = changelogFile.exists() ? changelogFile.text : ''

tasks.register('publishCurseForge', TaskPublishCurseForge) {
    apiToken = System.getenv('CURSEFORGE_TOKEN')
    upload('918314', jar) {
        changelogType = 'markdown'
        changelog = changelogText
        releaseType = 'release'
        displayName = modName + ' - ' + modVersion + ' Fabric'
        addRequirement 'phantom-loader'
        addModLoader 'Fabric'
        addGameVersion minecraftVersion
    }
}

modrinth {
    token = System.getenv('MODRINTH_TOKEN')
    projectId = 'underground-jungle'
    versionNumber = modVersion
    versionName = modName + ' - ' + modVersion + ' Fabric'
    versionType = 'release'
    uploadFile = remapJar
    gameVersions = [minecraftVersion]
    loaders = ['fabric']
    changelog = changelogText
    dependencies {
        required.project 'phantom-loader'
        required.project 'fabric-api'
    }
}